#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.СписокРеализаций.Очистить(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен период формирования документов.";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПроверитьЗаполнениеКонстант()) Тогда
		Возврат;
	КонецЕсли;
		
	ДлительнаяОперация = НачатьВыполнениеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеКонстант()
	
	ТекстУведомления = "";
	Если Не ЗначениеЗаполнено(Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить()) Тогда
		ТекстУведомления = "Не заполнена константа Номенклатура Абонентская плата.";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить()) Тогда
		Если ТекстУведомления = "" Тогда
			ТекстУведомления = "Не заполнена константа Номенклатура Работы специалиста.";
		Иначе
			НовыйТекстУведомления = "Не заполнена константа Номенклатура Работы специалиста.";
			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(ТекстУведомления);
			МассивСтрок.Добавить(НовыйТекстУведомления);
			ТекстУведомления = СтрСоединить(МассивСтрок, Символы.ПС);
		КонецЕсли;			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстУведомления) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстУведомления);
		Возврат ТекстУведомления;
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьВыполнениеНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьСписокНаСервере", 
	Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеализацийМассив = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ЗаполнитьСписокРеализаций(СписокРеализацийМассив);
	
КонецПроцедуры  

&НаКлиенте
Процедура  ЗаполнитьСписокРеализаций(СписокРеализацийМассив);
	
	Объект.СписокРеализаций.Очистить(); 
	
	Для Каждого СписокРеализацияСтруктура Из СписокРеализацийМассив Цикл 
		НоваяСтрока = Объект.СписокРеализаций.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СписокРеализацияСтруктура);
		Если СписокРеализацияСтруктура.Реализация = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю;
		    Сообщение.Текст = СтрШаблон("По договору ""%1"" не удалось создать документ. Подробности в журнале регистрации.", НоваяСтрока.Договор);
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
